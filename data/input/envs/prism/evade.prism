pomdp

observables
start, dx, dy, turn
endobservables

const int RADIUS=2;

const int N=6;
const int xMAX = N-1;
const int yMAX = N-1;
const int aXMAX = xMAX;
const int aYMAX = yMAX;
const int aXMIN = 0;
const int aYMIN = 1;
const int dxMAX = xMAX;
const int dyMAX = yMAX;
const int dxMIN = 0;
const int dyMIN = 0;
const double sl;

formula done = start & dx = dxMAX & dy = dyMAX;
observable "amdone" = done;
formula crash =  (dx = ax & dy = ay);
observable "hascrash" = crash;
formula seedrone = ax-dx < RADIUS + 1 & dx-ax < RADIUS +1 & ay-dy < RADIUS+1 & dy -ay < RADIUS+1;
observable "seedx" = (seedrone |justscanned) ? ax : -1;
observable "seedy" = (seedrone |justscanned)  ? ay : -1;


module master
    start : bool init false;
    turn : bool init false;
    justscanned : bool init false;

    [placement] !start -> (start'=true);
    [north] start & turn -> (turn'=!turn);
    [south] start & !done & turn -> (turn'=!turn);
    [east]  start & !done & turn -> (turn'=!turn);
    [west]  start & !done & turn -> (turn'=!turn);
    [adv]  start & !done & !turn -> (turn'=!turn) & (justscanned'=false);
    [scan] start & !done & turn -> (justscanned'=true);
endmodule


module drone
    dx : [dxMIN..dxMAX] init 0;
    dy : [dyMIN..dyMAX] init 0;

    [west] true -> (1-sl): (dx'=max(dx-1,dxMIN)) + sl: (dx'=max(dx,dxMIN));
    [east] true -> (1-sl): (dx'=min(dx+1,dxMAX)) + sl: (dx'=min(dx,dxMAX));
    [south]  true -> (1-sl): (dy'=min(dy+1,dyMAX)) + sl: (dy'=min(dy,dyMAX));
    [north]  true -> (1-sl): (dy'=max(dy-1,dyMIN)) + sl: (dy'=max(dy,dyMIN));
    [scan] true -> 1:(dx'=dx);
endmodule



module agent
    ax : [aXMIN..aXMAX] init aXMAX-1;
    ay : [aYMIN..aYMAX] init aYMAX;

    [adv] true -> 1/8 : (ax'=max(ax-1,aXMIN)) +  1/8: (ax'=min(ax+1,aXMAX))
                + 1/8 : (ay'=max(ay-1,aYMIN)) + 1/8 : (ay'=min(ay+1,aYMAX))
                + 1/16 : (ax'=max(ax-2,aXMIN)) +  1/16: (ax'=min(ax+2,aXMAX))
                + 1/16 : (ay'=max(ay-2,aYMIN)) + 1/16 : (ay'=min(ay+2,aYMAX))
                + 1/16 : (ax'=max(ax-1,aXMIN)) & (ay'=max(ay-1,aYMIN)) +  1/16: (ax'=min(ax+1,aXMAX)) & (ay'=max(ay-1,aYMIN))
                + 1/16 : (ax'=max(ax-1,aXMIN)) & (ay'=min(ay+1,aYMAX)) +  1/16: (ax'=min(ax+1,aXMAX)) & (ay'=min(ay+1,aYMAX));
endmodule


label "goal" = done;
label "traps" = crash;
label "notbad" =  !crash;

rewards
    [north] true : 0.01;
    [south] true : 0.01;
    [west] true : 0.01;
    [east] true : 0.01;
    [scan] true : 0.01;
    crash = true:00; 
endrewards
